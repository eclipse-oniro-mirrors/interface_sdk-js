# Copyright (c) 2021-2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/config/components/ets_frontend/ets2abc_config.gni")
import("//build/ohos.gni")
import("//build/ohos/notice/notice.gni")
import("//build/ohos_var.gni")
import("//build/templates/bpf/ohos_bpf_config.gni")
import("//build/templates/metadata/module_info.gni")
import("interface_config.gni")

# 全局变量方法见 https://gitee.com/openharmony/build/blob/master/docs/cmake%E8%BD%ACgn%E6%8C%87%E5%AF%BC%E6%96%87%E6%A1%A3.md#gn%E5%B8%B8%E7%94%A8%E7%9A%84%E5%86%85%E7%BD%AE%E5%8F%98%E9%87%8F

# 将ets1.2文件处理成ets1.2interop
action("interface_sdk_interop2") {
  interface_sdk_interop2_deps = ""
  if (host_toolchain == toolchain_mac) {
    interface_sdk_interop2_deps = ":build_ohos_ets($toolchain_mac)"
  } else if (host_toolchain == toolchain_win) {
      interface_sdk_interop2_deps = ":build_ohos_ets($toolchain_win)"
  } else {
    interface_sdk_interop2_deps = ":build_ohos_ets($toolchain_linux)"
  }
  deps = [interface_sdk_interop2_deps]
  script = "//interface/sdk-js/compile_ets_ts.py"
  if (host_os == "mac") {
    node_path =
        "//prebuilts/build-tools/common/nodejs/node-v16.20.2-darwin-x64/bin/node"
  } else {
    node_path =
        "//prebuilts/build-tools/common/nodejs/node-v16.20.2-linux-x64/bin/node"
  }
  args=[
    "--root-build-dir",
    rebase_path("//", root_build_dir),
    "--tool-dir",
    rebase_path(build_sdk_path, root_build_dir),
    "--output-interface-sdk",
    rebase_path(interface_sdk_path, root_build_dir),
    "--node-path",
    rebase_path(node_path, root_build_dir)
  ]
  outputs = [
    interface_sdk_path + "/ets1.2interop/declaration",
    interface_sdk_path + "/ets1.2interop/bridge"
  ]
}

# 特殊场景没有拷贝interface接口信息，强基之后源码仓api不能直接使用，需要进行处理
action("ohos_base_split") {
  script = "//build/ohos/sdk/parse_interface_sdk.py"
  ohos_sdk_arkts_description_file =
      "${interface_sdk_path}/ohos_sdk_pub_description_std.json"
  if (host_os == "mac") {
    node_path = "//prebuilts/build-tools/common/nodejs/node-v16.20.2-darwin-x64/bin/node"
    npm_path = "//prebuilts/build-tools/common/nodejs/node-v16.20.2-darwin-x64/bin/npm"
  } else {
    node_path = "//prebuilts/build-tools/common/nodejs/node-v16.20.2-linux-x64/bin/node"
    npm_path = "//prebuilts/build-tools/common/nodejs/node-v16.20.2-linux-x64/bin/npm"
  }
  args = [
    "--sdk-description-file",
    rebase_path("//build/ohos/sdk/ohos_sdk_description_std.json",
                root_build_dir),
    "--root-build-dir",
    rebase_path("//", root_build_dir),
    "--node-js",
    rebase_path(node_path, root_build_dir),
    "--output-arkts-sdk-desc-file",
    rebase_path(ohos_sdk_arkts_description_file, root_build_dir),
    "--sdk-build-public",
    "${sdk_build_public}",
    "--sdk-build-arkts",
    "${sdk_build_arkts}",
    "--npm-path",
    rebase_path(npm_path, root_build_dir),
    "--output-interface-sdk",
    rebase_path(interface_sdk_path, root_build_dir),
  ]
  outputs = [ "${root_out_dir}/sdk-interface" ]
}

template("ohos_copy_internal") {
  forward_variables_from(invoker, "*")
  iv_input = invoker.iv_input
  input_project_dir = invoker.input_project_dir
  _module_info_target = "${target_name}_info"

  # 调用build/templates/common/copy.gni中的ohos_copy方法
  # 将处理完成的文件输出到中间产物对应位置 out/sdk/obj/interface/sdk-js/${target_name}
  action_with_pydeps(target_name) {
    # 该脚本根据传入的remove文件进行input文件规则检查，过滤不需要的文件
    # remove文件没有对应$target_name的属性  则全部输出
    # remove文件有对应$target_name的属性    保留base中的文件；
    #                                      删除global_remove中的文件；
    #                                     ispublic为真，删除sdk_build_public_remove文件。
    deps = [ ":ohos_base_split" ]
    # 在打包编译ets1.2时才会打包出interop文件
    if (sdk_build_arkts) {
      deps +=[ ":interface_sdk_interop2",
      ":install_tsc_declgen" ]
    }
    deps += [ ":$_module_info_target" ]
    inputs = []
    outputs = [ target_out_dir + "/${sdk_type}/${target_name}" ]
    script = "//interface/sdk-js/process_internal.py"
    args = [
      "--input",
      rebase_path(iv_input, root_build_dir),
      "--project-dir",
      rebase_path(input_project_dir, root_build_dir),
      "--base-dir",
      rebase_path("//interface/sdk-js", root_build_dir),
      "--remove",
      rebase_path("//interface/sdk-js/remove_list.json", root_build_dir),
      "--ispublic",
      "${sdk_build_public}",
      "--name",
      "${target_name}",
      "--output",
      rebase_path(target_out_dir + "/${sdk_type}/${target_name}",
                  root_build_dir),
    ]
  }
  _target_name = target_name
  generate_module_info(_module_info_target) {
    module_type = "jsdoc"
    module_install_name = ""
    module_name = _target_name
    module_source_dir = target_out_dir + "/${sdk_type}/${_target_name}"
    install_enable = false
  }
}

# 主要api处理template
template("ohos_declaration_template") {
  forward_variables_from(invoker, "*")
  input_project_dir = invoker.input_project_dir
  _module_info_target = "/ohos_declaration/${sdk_type}/${target_name}_info"

  input_api_dir = input_project_dir + "/api"

  action_with_pydeps(target_name) {
    deps = [ ":ohos_base_split" ]
    # 在打包编译ets1.2时才会打包出interop文件
    if (sdk_build_arkts) {
      deps +=[ ":interface_sdk_interop2",
      ":install_tsc_declgen" ]
    }
    deps += [ ":$_module_info_target" ]
    inputs = []
    outputs = [ root_out_dir + "/ohos_declaration/${sdk_type}/${target_name}" ]

    # 处理api文件下全部文件，过滤特定文件
    script = "//interface/sdk-js/remove_internal.py"
    args = [
      "--input",
      rebase_path(input_api_dir, root_build_dir),
      "--output",
      rebase_path(
          root_out_dir + "/ohos_declaration/${sdk_type}/${target_name}/",
          root_build_dir),
    ]
  }
  _target_name = target_name
  generate_module_info(_module_info_target) {
    module_type = "jsdoc"
    module_install_name = ""
    module_name = _target_name
    module_source_dir =
        root_out_dir + "/ohos_declaration/${sdk_type}/$_target_name"
    install_enable = false
  }
}

# 运行处理ets1.2interop工具所需要的依赖
group("build_ohos_ets") {
  external_deps = [
    ohos_ets_api_deps,
    ohos_ets_arkts_deps,
    ohos_ets_build_system_deps,
    ohos_ets_kits_deps,
    ohos_ets_stdlib_deps
  ]
  deps = [
    ":ohos_base_split",
    ohos_ets_koala_wrapper_deps,  
    ohos_ets_ark_link_deps,
    ohos_ets_es2panda_deps,
    ohos_ets_libes2panda_public_deps,
    ohos_ets_ui_plugins_deps
  ]
}

# ets/api执行脚本
ohos_declaration_template("ohos_declaration_ets") {
  sdk_type = "ets"
  input_project_dir = interface_sdk_path_ets1
}

# ets1interop/api执行脚本
ohos_declaration_template("ohos_declaration_ets_interop") {
  sdk_type = "ets"
  input_project_dir = interface_sdk_path_ets1_interop
}

# ets2/api执行脚本
ohos_declaration_template("ohos_declaration_ets2") {
  sdk_type = "ets2"
  input_project_dir = interface_sdk_path_ets2
}

# ets2interop/api声明文件执行脚本
ohos_declaration_template("ohos_declaration_ets2_interop") {
  sdk_type = "ets2"
  input_project_dir = interface_sdk_path_ets2_interop + "/declaration"
}

# ets2interop/api胶水代码执行脚本
ohos_declaration_template("ohos_declaration_ets2_interop_bridge") {
  sdk_type = "ets2"
  input_project_dir = interface_sdk_path_ets2_interop + "/bridge"
}

# ets/api/@internal/full执行脚本
ohos_copy_internal("ets_internal_api") {
  sdk_type = "ets"
  iv_input = interface_sdk_path_ets1 + "/api/@internal/ets"
  input_project_dir = interface_sdk_path_ets1
}

# ets1interop/api/@internal/full执行脚本
ohos_copy_internal("ets_internal_api_interop") {
  sdk_type = "ets"
  iv_input = interface_sdk_path_ets1_interop + "/api/@internal/ets"
  input_project_dir = interface_sdk_path_ets1_interop
}

# ets/arkts执行脚本
ohos_copy_internal("bundle_arkts") {
  sdk_type = "ets"
  iv_input = interface_sdk_path_ets1 + "/arkts"
  input_project_dir = interface_sdk_path_ets1
}

# ets1interop/arkts执行脚本
ohos_copy_internal("bundle_arkts_interop") {
  sdk_type = "ets"
  iv_input = interface_sdk_path_ets1_interop + "/arkts"
  input_project_dir = interface_sdk_path_ets1_interop
}

# ets1.2/arkts执行脚本
ohos_copy_internal("bundle_arkts_ets1.2") {
  sdk_type = "ets2"
  iv_input = interface_sdk_path_ets2 + "/arkts"
  input_project_dir = interface_sdk_path_ets2
}

# ets1.2interop/arkts声明文件执行脚本
ohos_copy_internal("bundle_arkts_ets1.2_interop") {
  sdk_type = "ets2"
  iv_input = interface_sdk_path_ets2_interop + "/declaration/arkts"
  input_project_dir = interface_sdk_path_ets2_interop + "/declaration"
}

# ets1.2interop/arkts胶水代码执行脚本
ohos_copy_internal("bundle_arkts_ets1.2_interop_bridge") {
  sdk_type = "ets2"
  iv_input = interface_sdk_path_ets2_interop + "/bridge/arkts"
  input_project_dir = interface_sdk_path_ets2_interop + "/bridge"
}

ohos_copy_internal("ets_internal_api2") {
  sdk_type = "ets2"
  iv_input = interface_sdk_path_ets2 + "/api/@internal/ets"
  input_project_dir = interface_sdk_path_ets2
}

ohos_copy_internal("ets_internal_api1.2_interop") {
  sdk_type = "ets2"
  iv_input = interface_sdk_path_ets2_interop + "/declaration/api/@internal/ets"
  input_project_dir = interface_sdk_path_ets2_interop + "/declaration"
}

ohos_copy_internal("ets_internal_api1.2_interop_bridge") {
  sdk_type = "ets2"
  iv_input = interface_sdk_path_ets2_interop + "/bridge/api/@internal/ets"
  input_project_dir = interface_sdk_path_ets2_interop + "/bridge"
}

if (!sdk_build_public) {
  # ets/build-tools/ets-loader/declarations脚本
  ohos_copy("bundle_api") {
    sdk_type = "ets"
    sources = [ "api/bundle/bundleStatusCallback.d.ts" ]
    outputs =
        [ target_out_dir + "/${sdk_type}/${target_name}/{{source_file_part}}" ]
    module_source_dir = target_out_dir + "/${sdk_type}/${target_name}"
    module_install_name = ""
  }
}

# ets/component执行脚本
ohos_copy_internal("ets_component") {
  sdk_type = "ets"
  iv_input = interface_sdk_path_ets1 + "/api/@internal/component/ets"
  input_project_dir = interface_sdk_path_ets1
}

# ets1interop/component执行脚本
ohos_copy_internal("ets_component_interop") {
  sdk_type = "ets"
  iv_input = interface_sdk_path_ets1_interop + "/api/@internal/component/ets"
  input_project_dir = interface_sdk_path_ets1_interop
}

# ets1.2/arkui/component执行脚本
ohos_copy_internal("ets_component2") {
  sdk_type = "ets2"
  iv_input = interface_sdk_path + "/arkui_transformer_api"
  input_project_dir = interface_sdk_path
}

ohos_copy_internal("ets_component2_interop") {
  sdk_type = "ets2"
  iv_input = interface_sdk_path_ets2_interop + "/declaration/api/@internal/component/ets"
  input_project_dir = interface_sdk_path_ets2_interop + "/declaration"
}

ohos_copy_internal("ets_component2_interop_bridge") {
  sdk_type = "ets2"
  iv_input = interface_sdk_path_ets2_interop + "/bridge/api/@internal/component/ets"
  input_project_dir = interface_sdk_path_ets2_interop + "/bridge"
}

# ets/kits执行脚本
ohos_copy_internal("bundle_kits") {
  sdk_type = "ets"
  iv_input = interface_sdk_path_ets1 + "/kits"
  input_project_dir = interface_sdk_path_ets1
}

# ets1interop/kits执行脚本
ohos_copy_internal("bundle_kits_interop") {
  sdk_type = "ets"
  iv_input = interface_sdk_path_ets1_interop + "/kits"
  input_project_dir = interface_sdk_path_ets1_interop
}

# ets2/kits执行脚本
ohos_copy_internal("bundle_kits2") {
  sdk_type = "ets2"
  iv_input = interface_sdk_path_ets2 + "/kits"
  input_project_dir = interface_sdk_path_ets2
}

ohos_copy_internal("bundle_kits2_interop") {
  sdk_type = "ets2"
  iv_input = interface_sdk_path_ets2_interop + "/declaration/kits"
  input_project_dir = interface_sdk_path_ets2_interop + "/declaration"
}

ohos_copy_internal("bundle_kits2_interop_bridge") {
  sdk_type = "ets2"
  iv_input = interface_sdk_path_ets2_interop + "/bridge/kits"
  input_project_dir = interface_sdk_path_ets2_interop + "/bridge"
}

# js/api执行脚本
ohos_declaration_template("ohos_declaration_common") {
  sdk_type = "ets"
  input_project_dir = interface_sdk_path_ets1
}

# js/api/@internal/full执行脚本
ohos_copy_internal("internal_full") {
  iv_input = interface_sdk_path_ets1 + "/api/common/full"
  input_project_dir = interface_sdk_path_ets1
}

# js/api/@internal/lite执行脚本呢
ohos_copy_internal("internal_lite") {
  iv_input = interface_sdk_path_ets1 + "/api/common/lite"
  input_project_dir = interface_sdk_path_ets1
}

# js/api/config执行脚本
ohos_copy("config") {
  sources = [
    "api/config/css",
    "api/config/hml",
  ]
  outputs = [ target_out_dir + "/${target_name}/{{source_file_part}}" ]
  module_source_dir = target_out_dir + "/${target_name}"
  module_install_name = ""
}

# js/form执行脚本
ohos_copy("form_declaration") {
  sources = [
    "api/form/action",
    "api/form/css",
    "api/form/hml",
  ]
  outputs = [ target_out_dir + "/${target_name}/{{source_file_part}}" ]
  module_source_dir = target_out_dir + "/${target_name}"
  module_install_name = ""
}

# toolchains/syscapcheck执行脚本
ohos_copy("syscap_check") {
  sources = [ "api/syscapCheck/sysCapSchema.json" ]
  outputs = [ target_out_dir + "/${target_name}/{{source_file_part}}" ]
  module_source_dir = target_out_dir + "/${target_name}"
  module_install_name = ""
}

action("ohos_ets_api_tmp") {
  script = "//interface/sdk-js/ohos_copy_ets.py"

  args = [
    "--input",
    rebase_path("//interface/sdk-js/api"),
    "--output",
    rebase_path("$ohos_ets_api_tmp_path"),
    "--type",
    "ets2",
    "--source-root-dir",
    rebase_path("//"),
    "--node-js",
    rebase_path(nodejs, root_build_dir),
  ]

  outputs = [ "$ohos_ets_api_tmp_path" ]
}

action("ohos_ets_arkts_tmp") {
  script = "//interface/sdk-js/ohos_copy_ets.py"

  args = [
    "--input",
    rebase_path("//interface/sdk-js/arkts"),
    "--output",
    rebase_path("$ohos_ets_arkts_tmp_path"),
    "--type",
    "ets2",
    "--source-root-dir",
    rebase_path("//"),
    "--node-js",
    rebase_path(nodejs, root_build_dir),
  ]

  outputs = [ "$ohos_ets_arkts_tmp_path" ]
}

action("ohos_ets_kits_tmp") {
  script = "//interface/sdk-js/ohos_copy_ets.py"

  args = [
    "--input",
    rebase_path("//interface/sdk-js/kits"),
    "--output",
    rebase_path("$ohos_ets_kits_tmp_path"),
    "--type",
    "ets2",
    "--source-root-dir",
    rebase_path("//"),
    "--node-js",
    rebase_path(nodejs, root_build_dir),
  ]

  outputs = [ "$ohos_ets_kits_tmp_path" ]
}

action("ohos_ets_api_arkui_tmp") {
  deps = [ ":ohos_ets_api_tmp" ]
  script = "//interface/sdk-js/arkui_transformer.py"

  if (host_os == "mac") {
    node_path = "//prebuilts/build-tools/common/nodejs/node-v16.20.2-darwin-x64/bin/node"
    npm_path =
        "//prebuilts/build-tools/common/nodejs/node-v16.20.2-darwin-x64/bin/npm"
  } else {
    node_path =
        "//prebuilts/build-tools/common/nodejs/node-v16.20.2-linux-x64/bin/node"
    npm_path =
        "//prebuilts/build-tools/common/nodejs/node-v16.20.2-linux-x64/bin/npm"
  }
  args = [
    "--input",
    rebase_path(ohos_ets_api_tmp_path + "/@internal/component/ets",
                root_build_dir),
    "--output",
    rebase_path(ohos_ets_api_tmp_path + "/arkui/component", root_build_dir),
    "--source_root_dir",
    rebase_path("//", root_build_dir),
    "--npm-path",
    rebase_path(npm_path, root_build_dir),
    "--node-js",
    rebase_path(node_path, root_build_dir),
  ]
  outputs = [ "$ohos_ets_api_arkui_tmp_path" ]
}

ohos_copy("ohos_ets_arkts") {
  deps = [ ":ohos_ets_arkts_tmp" ]
  sources = [ ohos_ets_arkts_tmp_path ]
  outputs = [ ohos_ets_arkts_path ]
  part_name = "sdk"
  subsystem_name = "sdk"
}

ohos_copy("ohos_ets_kits") {
  deps = [ ":ohos_ets_kits_tmp" ]
  sources = [ ohos_ets_kits_tmp_path ]
  outputs = [ ohos_ets_kits_path ]
  part_name = "sdk"
  subsystem_name = "sdk"
}

ohos_copy("ohos_ets_api") {
  deps = [
    ":ohos_ets_api_arkui_tmp",
    ":ohos_ets_api_tmp",
  ]
  sources = [ ohos_ets_api_tmp_path ]
  outputs = [ ohos_ets_api_path ]
  part_name = "sdk"
  subsystem_name = "sdk"
}

action("install_tsc_declgen") {
  deps = [
  ":ohos_base_split"
  ]
  external_deps = [
  "runtime_core:declgen_ts2sts_compile_declgen"
  ]
  script="run_compile_declgen.py"
  args = [
    "--root-build-dir",
    rebase_path("//", root_build_dir),
    "--current-os-dir",
    current_os,
    "--output-interface-sdk",
    rebase_path(interface_sdk_path, root_build_dir),
    "--tool-dir",
    rebase_path(
    "${root_out_dir}/obj/arkcompiler/runtime_core/static_core/plugins/ets/tools/declgen_ts2sts/panda-declgen-1.0.0.tgz"),
    "--node-js",
    rebase_path(nodejs, root_build_dir),
  ]
  outputs = [ "${interface_sdk_path}/ets1.1interop" ]
}

action("gen_api_check_plugin") {
  deps = [
    "//developtools/ace_ets2bundle/arkui-plugins:ui_plugin"
  ]
  npm_path = "//prebuilts/build-tools/common/nodejs/current/bin/npm"
  script = "build_api_check_plugin.py"
  args = [
    "--source_path",
    rebase_path(get_path_info("./build-tools/compiler-plugins/api-check-plugin-static", "abspath")),
    "--output_path",
    rebase_path("$target_gen_dir"),
    "--npm",
    rebase_path(npm_path),
  ]
  outputs = [ "$target_gen_dir" ]
}

ohos_copy("api_check_plugin") {
  deps = [
    ":gen_api_check_plugin"
  ]
  sources = [
    rebase_path("$target_gen_dir")
  ]
  outputs = [ target_out_dir + "/$target_name" ]
  module_source_dir = target_out_dir + "/$target_name"
  module_install_name = ""
}